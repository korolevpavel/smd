метод СписокСотрудниковGETОбработкаЗапроса(Запрос: HttpСервисЗапрос)
    
    знч МассивСотрудников = Сотрудники.ПолучитьМассивСотрудников()
    Запрос.Ответ.Заголовки.Установить("Content-type", "application/json")
    Запрос.Ответ.УстановитьКодСтатуса(200)
    Запрос.Ответ.УстановитьТело(СериализацияJson.ЗаписатьОбъект(МассивСотрудников))
    
;

метод СписокСотрудниковPOSTОбработкаЗапроса(Запрос: HttpСервисЗапрос)
   
    исп КонтекстДоступа.Привилегированный()

    знч ДанныеСтрокой = Запрос.Тело.ПрочитатьКакСтроку()

    пер Данные: Массив<Сотрудники.Сотрудник>

    попытка
        Данные = СериализацияJson.ПрочитатьОбъект(ДанныеСтрокой, Данные.ПолучитьТип())
    поймать Исключение: Исключение
        Запрос.Ответ.УстановитьКодСтатуса(400)
        Запрос.Ответ.УстановитьТело("Не удалось разобрать json: %{Исключение.Описание}")
        возврат
    ;

    Данные.ДляКаждого(Сотрудник -> Сотрудники.ЗаписатьОбновитьСотрудника(Сотрудник))
    Запрос.Ответ.УстановитьТело("Done!")
    
;